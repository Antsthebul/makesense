SELECT * WHERE {
  ?lexeme wikibase:lemma ?lemma;
         dct:language ?lang;
         wikibase:lexicalCategory ?cat;
         wdt:P5185 ?genus.
  FILTER NOT EXISTS {
    ?lexeme ontolex:sense ?sense.
    ?sense wdt:P5137 ?item.
  }.
  FILTER NOT EXISTS { ?lexeme wdt:P5402 ?homograph }.
  
  ?item rdfs:label ?lemma.
  ?item schema:description ?desc.	FILTER(LANG(?desc) = LANG(?lemma)).
  FILTER NOT EXISTS { ?item a wikibase:Property}.
  # Filter out common false-positives
  FILTER NOT EXISTS { ?item wdt:P131 ?_ }.
  FILTER NOT EXISTS {
    VALUES ?not { wd:Q4167410 wd:Q101352 wd:Q11424 wd:Q3305213 wd:Q482994 wd:Q13417114 wd:Q7366 wd:Q208569 wd:Q15632617 wd:Q21191270 wd:Q5185279 }.
    ?item wdt:P31 ?not
  }
}
